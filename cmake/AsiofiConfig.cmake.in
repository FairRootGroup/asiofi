################################################################################
#    Copyright (C) 2018 GSI Helmholtzzentrum fuer Schwerionenforschung GmbH    #
#                                                                              #
#              This software is distributed under the terms of the             #
#              GNU Lesser General Public Licence (LGPL) version 3,             #
#                  copied verbatim in the file "LICENSE"                       #
################################################################################

cmake_minimum_required(VERSION 3.10 FATAL_ERROR)

@PACKAGE_INIT@

### General variables for project discovery/inspection
set(ASIOFI_VERSION @PROJECT_VERSION@)
set(ASIOFI_GIT_VERSION @ASIOFI_GIT_VERSION@)

set_and_check(Asiofi_ROOT @PACKAGE_CMAKE_INSTALL_PREFIX@)
set_and_check(Asiofi_BINDIR @PACKAGE_CMAKE_INSTALL_PREFIX@/@Asiofi_INSTALL_BINDIR@)
set_and_check(Asiofi_INCDIR @PACKAGE_CMAKE_INSTALL_PREFIX@/@Asiofi_INSTALL_INCDIR@)
set_and_check(Asiofi_LIBDIR @PACKAGE_CMAKE_INSTALL_PREFIX@/@Asiofi_INSTALL_LIBDIR@)
set_and_check(Asiofi_DATADIR @PACKAGE_CMAKE_INSTALL_PREFIX@/@Asiofi_INSTALL_DATADIR@)
set_and_check(Asiofi_CMAKEMODDIR @PACKAGE_CMAKE_INSTALL_PREFIX@/@Asiofi_INSTALL_CMAKEMODDIR@)

### Import cmake modules
set(CMAKE_MODULE_PATH ${Asiofi_CMAKEMODDIR} ${CMAKE_MODULE_PATH})

### Package dependencies
include(CMakeFindDependencyMacro)

if(    NOT (Asiofi_BOOST_DISABLED OR Asiofi_PACKAGE_DEPENDENCIES_DISABLED)
   AND (@Boost_FOUND@ OR Asiofi_Boost_REQUIRED))
  set(Asiofi_Boost_COMPONENTS system)
  if(Asiofi_ADDITIONAL_Boost_COMPONENTS)
    list(APPEND Asiofi_Boost_COMPONENTS ${Asiofi_ADDITIONAL_Boost_COMPONENTS})
    list(REMOVE_DUPLICATES Asiofi_Boost_COMPONENTS)
  endif()
  if(Boost_INCLUDE_DIR) # checks for cached boost variable which indicates if Boost is already found
    set(SILENCE_BOOST QUIET)
  endif()
  find_dependency(Boost 1.64 ${SILENCE_BOOST}
    COMPONENTS ${Asiofi_Boost_COMPONENTS}
  )
endif()

if(    NOT (Asiofi_OFI_DISABLED OR Asiofi_PACKAGE_DEPENDENCIES_DISABLED)
   AND (@OFI_FOUND@ OR Asiofi_OFI_REQUIRED))
  set(Asiofi_OFI_COMPONENTS fi_sockets)
  if(Asiofi_ADDITIONAL_OFI_COMPONENTS)
    list(APPEND Asiofi_OFI_COMPONENTS ${Asiofi_ADDITIONAL_OFI_COMPONENTS})
    list(REMOVE_DUPLICATES Asiofi_OFI_COMPONENTS)
  endif()
  find_dependency(OFI 1.6.0
    COMPONENTS ${Asiofi_OFI_COMPONENTS}
  )
endif()

### Import targets
include(@PACKAGE_CMAKE_INSTALL_PREFIX@/@PACKAGE_INSTALL_DESTINATION@/AsiofiTargets.cmake)

check_required_components(Asiofi)
